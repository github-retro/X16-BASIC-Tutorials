10 REM TUTORIAL FOR TILE AND TILE MOVEMENT
20 REM BY JUSTIN BALDOCK

100 GOSUB 10000: REM SETUP DISPLAY
110 GOSUB 20000: REM SETUP TILE MAP
120 GOSUB 30000: REM SETUP TILE GRAPHICS
130 GOSUB 1000:  REM THE MAIN LOOP
140 END

1000 REM MAIN LOOP
1010 LET TH=0: TV=0: REM TILE HORIZONTAL AND VERTICAL LOCATION 0-4095
1020 LET HH=0: HL=0: VH=0: VL=0: REM HIGH AND LOW VALUES FOR HOR AND VER
1030 LET MV=2: REM SET DEFAULT MOVEMENT SPEED
1040 GET K$: REM READ IN KEYPRESSES
1050 IF K$="A" THEN GOSUB 1100: REM MOVE LEFT
1060 IF K$="D" THEN GOSUB 1200: REM MOVE RIGHT
1070 IF K$="W" THEN GOSUB 1300: REM MOVE UP
1080 IF K$="S" THEN GOSUB 1400: REM MOVE DOWN
1090 IF K$="Q" THEN RETURN: REM EXIT SUBROUTINE
1095 GOTO 1040: REM LOOP BACK TO GET KEYBOARD INPUT

1100 REM MOVE VIEW LEFT
1110 IF TH = 0 THEN RETURN
1120 TH=TH-MV
1130 GOSUB 1700: REM CALCULATE 8BIT 
1140 GOSUB 1900: REM UPDATE SCROLL 
1150 RETURN

1200 REM MOVE VIEW RIGHT
1210 IF TH = 3776 THEN RETURN: REM LAYER WIDTH - SCREEN VIEW WIDTH
1220 TH=TH+MV
1230 GOSUB 1700: REM CALCULATE 8BIT 
1240 GOSUB 1900: REM UPDATE SCROLL 
1250 RETURN 

1300 REM MOVE UP
1310 IF TV = 0 THEN RETURN
1320 TV=TV-MV
1330 GOSUB 1800: REM CALCULATE 8BIT
1340 GOSUB 1900: REM UPDATE SCROLL
1350 RETURN

1400 REM MOVE DOWN
1410 IF TV = 272 THEN RETURN: REM 512-240
1420 TV=TV+MV
1430 GOSUB 1800: REM CALCULATE 8BIT
1440 GOSUB 1900: REM UPDATE SCROLL
1450 RETURN

1700 REM CALCULATE HORITZONAL HIGH AND LOW VALUES TO POKE INTO LAYER SCROLL
1710 HH = INT(TH / 255)
1720 HL = TH - (HH * 255)
1730 RETURN

1800 REM CALCULATE VERTICAL HIGH AND LOW VALUES TO POKE INTO LAYER SCROLL
1810 VH = INT(TV / 255)
1820 VL = TV - (VH * 255)
1830 RETURN

1900 REM UPDATE VERA FOR NEW LAYER SCROLL
1910 POKE $9F30,HL
1920 POKE $9F31,HH
1930 POKE $9F32,VL 
1940 POKE $9F33,VH
1950 RETURN 

10000 REM SETUP DISPLAY
10010 SCREEN $00: REM SETUP SCREEN MODE 320X240
10020 REM CONFIGURE LAYER 0 AND DISPLAY TILES
10030 POKE $9F29, %00110001: REM TURN ON LAYER 0,1 AND VGA OUTPUT
10040 POKE $9F2D, %00110010: REM MAP 32 X 256, T256C, BITMAP MODE, COLOR DEPTH
10050 POKE $9F2E, %00100000: REM MAP BASE AT $4000
10060 POKE $9F2F, %10000011: REM SET TILE BASE AT $10000 AND H=16 W=16
10070 REM CONFIGURE LAYER 1 AND DISPLAY INSTRUCTIONS
10080 COLOR 1,0
10090 CLS
10100 PRINT "MOVE VIEW USING WASD. Q TO QUIT"
10110 RETURN

20000 REM SETUP TILE MAP
20010 VLOAD "TILEMAP-256X32.BIN",8,0,$4000
20020 RETURN

30000 REM SETUP TILE GRAPHICS
30020 VLOAD "TILE-GFX.BIN",8,1,$0000
30030 RETURN

